{
  parserClass="de.unruh.onthefly.parser.SimpleParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Simple"
  psiImplClassSuffix="Impl"
  psiPackage="de.unruh.onthefly.psi"
  psiImplPackage="de.unruh.onthefly.psi.impl"

  elementTypeHolderClass="de.unruh.onthefly.psi.SimpleTypes"
  elementTypeClass="de.unruh.onthefly.psi.SimpleElementType"
  tokenTypeClass="de.unruh.onthefly.psi.SimpleTokenType"

  psiImplUtilClass="de.unruh.onthefly.psi.impl.SimplePsiImplUtil"
}

simpleFile ::= item_*

private item_ ::= (assignment|COMMENT|CRLF)

assignment ::= (IDENTIFIER ASSIGN expression)
    {methods=[getVariable getInlay]
     implements="de.unruh.onthefly.inlays.PsiWithInlay"}

atom ::= (variable | numeral)
    {methods=[getExpression]
     implements="de.unruh.onthefly.PsiExpression"}

variable ::= IDENTIFIER
    {methods=[getExpression]
     implements="de.unruh.onthefly.PsiExpression"}

numeral ::= INT
    {methods=[getExpression]
     implements="de.unruh.onthefly.PsiExpression"}

expression ::= (infixExpression | prefixExpression)
    {methods=[getExpression]
     implements="de.unruh.onthefly.PsiExpression"}

infixOp ::= (PLUS|DIV|MINUS|TIMES)

infixExpression ::= (atom infixOp atom)
    {methods=[getExpression]
     implements="de.unruh.onthefly.PsiExpression"}

prefixOp ::= (MINUS|SQRT)

prefixExpression ::= (prefixOp atom)
    {methods=[getExpression]
     implements="de.unruh.onthefly.PsiExpression"}
